Proceso Burbuja
	Dimension lista[5];
	lista[1]=5;
	lista[2]=2;
	lista[3]=4;
	lista[4]=1;
	lista[5]=3;
	//Longitud de la lista
	lon=5;
	l=lon;
	
	mostrarLista(lista,lon);
    Hacer
		n=0;
		//Recorrer la lista
		Para i=2 Hasta l Con Paso 1 Hacer
			//Verificar si los dos valores estan ordenados
			Si lista[i-1]>lista[i]
				//Ordenar si es necesario
				temp=lista[i-1];
				lista[i-1]=lista[i];
				lista[i]=temp;
				n=i;
				mostrarLista(lista,lon);
			FinSi
			
		FinPara
		l=n;
	Hasta Que n=0;
FinProceso

//Función para mostrar estado de la lista
SubProceso mostrarLista(lista,lon)
	Para i=1 Hasta lon Con Paso 1 Hacer
		Escribir Sin Saltar lista[i] " ";
	FinPara
	Escribir "";
FinSubProceso
